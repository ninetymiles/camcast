plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

version baseVersion

android {
    namespace 'com.rex.camcast'
    compileSdk 36

    defaultConfig {
        applicationId "com.rex.camcast"

        minSdk 26
        targetSdk 36

        versionName = project.version.toString()
        versionCode Integer.parseInt(([ 'git','rev-list','--count','HEAD'].execute().text).trim())

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "default_server_uri", '""'
        resValue "integer", "default_resolution", '1'
    }

    signingConfigs {
        release {}
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig = signingConfigs.getByName("release")
        }
        debug {
            versionNameSuffix "-debug"
            resValue "string", "default_server_uri", '"rtmp://192.168.88.62:1935/live/1"'
            //resValue "string", "default_server_uri", '"srt://192.168.88.62:8080/publish/live"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        jniLibs {
            pickFirsts += ['**/*.so']
        }
        exclude "META-INF/**"
    }
    buildFeatures {
        viewBinding true
    }
    publishing {
        singleVariant('release') {
            publishApk()
        }
    }
}

// Provided in gradle.properties
if (project.hasProperty("signStoreFile")) {
    android.signingConfigs.release {
        storeFile = rootProject.file(signStoreFile)
        storePassword = signStorePassword
        keyAlias = signKeyAlias
        keyPassword = signKeyPassword
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation("androidx.documentfile:documentfile:1.1.0")
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.10.0'
    implementation("androidx.preference:preference-ktx:1.2.1")

    implementation 'com.google.android.material:material:1.13.0'

    implementation "io.github.thibaultbee.streampack:streampack-core:3.0.2"
    implementation "io.github.thibaultbee.streampack:streampack-ui:3.0.2"
    implementation "io.github.thibaultbee.streampack:streampack-rtmp:3.0.2"
    implementation "io.github.thibaultbee.streampack:streampack-srt:3.0.2"

    implementation("org.slf4j:slf4j-api:2.0.17")
    implementation("com.github.tony19:logback-android:3.0.0")

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}

//println("project.name: " + project.name)        // app
//println("project.group: " + project.group)      // CamCast
//println("project.version: " + project.version)  // 0.9.x
afterEvaluate {
    //println("components.names: " + components.names) // [release]
    publishing {
        publications {
            release(MavenPublication) {
                groupId = "rex"
                artifactId = "camcast"
                from(components.release)
            }
        }
        repositories {
            maven { url = uri(rootProject.file("release")) }
        }
    }
}